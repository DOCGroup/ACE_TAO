#ifndef RACE_CONDUCTOR_IDL
#define RACE_CONDUCTOR_IDL

#include <Components.idl>
#include "RACE/common/OpString.idl"
#include "RACE/common/RACE_common.idl"

#include "RACE/Data_Base/Data_Base.idl"
#include "RACE/Output_Adapters/DAnCE_Output_Adapter/DAnCE_Output_Adapter.idl"
#include "RACE/Controller/Base/Controller_Base.idl"

module CIAO
{
  module RACE
  {
    module Conductor
    {
      interface Admin
      {
        /// "Schedule" a string for future deployment.
        boolean add_string (in OperationalString opstring,
                            out UUID ID);

        /// Remove a string that was scheduled for deployment. Raises
        /// UnknownID.
        boolean delete_string (in UUID ID)
          raises (UnknownID);

        /// Deploy a string right away.
        boolean deploy_string (in OperationalString opstring,
                               out UUID ID);

        /// Deploy a string that was previously scheduled via
        /// add_string. Raises UnknownID.
        boolean deploy_string_with_id (in UUID ID)
          raises (UnknownID);

        /// Modify a previously scheduled/deployed string. Raises UnknownID.
        boolean modify_string (in OperationalString opstring,
                               in UUID ID)
          raises (UnknownID);

        /// Teardown a string. Raises UnknownID.
        boolean tear_down_string (in UUID ID)
          raises (UnknownID);

        /// Initialize the system.
        boolean init_system();

        /// Start the controller.
        boolean start_controller();
        
        /// Stop the controller.
        boolean stop_controller();

      };

      component Conductor_Component
      {
        provides Admin admin;

        uses Data_Base::Admin DB;

        uses DAnCE_OA::Admin OA;

        uses Controller::Admin controllerAdmin;

        uses Controller::Control_Operations controlOperations;
      };

      home Conductor_Component_Home manages Conductor_Component
      {
      };
    };
  };

};

#endif /* RACE_CONDUCTOR_IDL */
