// $Id$

/**
 * @file Shapes_Connector.idl
 * @author Marcel Smit <msmit@remedy.nl>
 *
 * by-hand translation of the idl3p.
 */

#ifndef SHAPES_CONNECTOR_IDL_
#define SHAPES_CONNECTOR_IDL_

#include  <Components.idl>
#include "dds4ccm/idl/dds4ccm_ListenerControl.idl3"
#include "dds4ccm/idl/dds4ccm_ListenerControlE.idl"
#include "dds4ccm/idl/dds4ccm_PortStatusListener.idl3"
#include "dds4ccm/idl/dds4ccm_PortStatusListenerE.idl"
#include "dds4ccm/idl/dds4ccm_ConnectorStatusListener.idl3"
#include "dds4ccm/idl/dds4ccm_ListenerControl.idl3"
#include "dds4ccm/idl/dds_rtf2_dcpsE.idl"
#include "Base/Shapes_Base.idl"

module CCM_DDS
{
  interface ShapeType_InstanceHandleManager {
    DDS::InstanceHandle_t register_instance (in ShapeType datum);
    void unregister_instance (in ShapeType datum, in DDS::InstanceHandle_t instance_handle);
  };

  // @from DDS_Write for Shapes::ShapeType
  interface ShapeType_Writer : ShapeType_InstanceHandleManager
  {
    void write (in ShapeType an_instance)
      raises (InternalError);
    void write_many (in ShapeType_Seq an_instance)
      raises (InternalError);
  };

  // @from DDS_Update for Shapes::ShapeType
  interface ShapeType_Updater
  {
    void create (in ShapeType an_instance)
        raises (AlreadyCreated,
                InternalError);
    void update(in ShapeType an_instance)
        raises (NonExistent,
                InternalError);
    void delete (in ShapeType an_instance)
        raises (NonExistent,
                InternalError);
    readonly attribute boolean is_lifecycle_checked;
    attribute boolean is_coherent_write;
  };

  interface ShapeType_Getter
  {
    boolean get_all (out ShapeType_Seq instances, out ReadInfoSeq infos)
            raises (InternalError);
    boolean get_all_history (out ShapeType_Seq instances, out ReadInfoSeq infos)
            raises (InternalError);
    boolean get_one (inout ShapeType an_instance, out ReadInfo info)
            raises (NonExistent,
                    InternalError);
    boolean get_one_history (in ShapeType an_instance,
                           out ShapeType_Seq instances, out ReadInfoSeq infos)
            raises (NonExistent,
                    InternalError);
    boolean get_next (out ShapeType an_instance, out ReadInfo info)
            raises (InternalError);
    attribute QueryFilter filter
            setraises (BadParameter);
    attribute DDS::Duration_t time_out;
  };

  // @from Reader<T> from DDS_RawListen for Shapes::ShapeType
  interface ShapeType_Reader
  {
    void read_all (out ShapeType_Seq instances, out ReadInfoSeq infos)
      raises (InternalError);

    void read_all_history (out ShapeType_Seq instances, out ReadInfoSeq infos)
      raises (InternalError);

    void read_one (inout ShapeType an_instance, out ReadInfo info)
      raises (NonExistent,
              InternalError);

    void read_one_history (in ShapeType an_instance,
                           out ShapeType_Seq instances, out ReadInfoSeq infos)
      raises (NonExistent,
              InternalError);

    attribute QueryFilter filter
      setraises (BadParameter);
  };

  // @from RawListener for Shapes::ShapeType
  interface ShapeType_RawListener
  {
    void on_data (in ShapeType an_instance, in ReadInfo info);
  };

  porttype DDS_Write {
    uses CCM_DDS::ShapeType_Writer data;
    uses DDS::DataWriter    dds_entity;
  };
  porttype DDS_Update {
    uses CCM_DDS::ShapeType_Updater data;
  };
  porttype DDS_Listen {
    uses CCM_DDS::ShapeType_Reader data;
    uses CCM_DDS::ShapeType_Getter get_data;
    uses CCM_DDS::ListenerControl control;
    provides CCM_DDS::ShapeType_RawListener listener;
    uses DDS::DataReader dds_entity;
    provides CCM_DDS::PortStatusListener status;
  };
};

module Shapes
{
  component Shapes_Connector
  {
    // @from DDS_TopicBase
    attribute string topic_name;
    readonly attribute DDS::StringSeq key_fields;

    // @from DDS_TopicBase : DDS_Base
    uses CCM_DDS::ConnectorStatusListener error_listener;
    attribute DDS::DomainId_t domain_id;
    attribute string qos_profile;

    // @from DDS_Write for ShapeType
    mirrorport CCM_DDS::DDS_Write info_in;

    // @from DDS_Update for ShapeType
    mirrorport CCM_DDS::DDS_Update info_update;

    // @from DDS_RawListen for ShapeType
    provides CCM_DDS::ShapeType_Reader info_out_data;
    provides CCM_DDS::ShapeType_Getter info_get_out_data;
    provides CCM_DDS::ListenerControl info_out_control;
    uses CCM_DDS::ShapeType_RawListener info_out_listener;
    provides DDS::DataReader info_out_dds_entity;
    uses CCM_DDS::PortStatusListener info_out_status;
  };
};


/*
For future use: IDL3 plus
#include <dds4ccm/idl/dds4ccm_Connector_Base.idl3p>
#include <dds4ccm/idl/dds4ccm_Port_RawListen.idl3p>
#include <dds4ccm/idl/dds4ccm_Port_Write.idl3p>

module Shapes
{
  connector Shapes_Connector : CCM_DDS::DDS_TopicBase
  {
    mirrorport CCM_DDS::DDS_Write <Shape_Info> info_in;
    mirrorport CCM_DDS::DDS_RawListen <Shape_Info> info_out;
  };
};
*/

#endif /* SHAPES_CONNECTOR_IDL_ */


