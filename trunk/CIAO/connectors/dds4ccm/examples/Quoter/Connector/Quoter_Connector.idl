// $Id$

/**
 * @file Quoter_Connector.idl
 * @author William R. Otte <wotte@dre.vanderbilt.edu>
 *
 * by-hand translation of the idl3p.
 */

#ifndef QUOTER_CONNECTOR_IDL_
#define QUOTER_CONNECTOR_IDL_

#include  <Components.idl>
#include "connectors/dds4ccm/idl/dds4ccm_ListenerControl.idl3"
#include "connectors/dds4ccm/idl/dds4ccm_ListenerControlE.idl"
#include "connectors/dds4ccm/idl/dds4ccm_PortStatusListener.idl3"
#include "connectors/dds4ccm/idl/dds4ccm_PortStatusListenerE.idl"
#include "connectors/dds4ccm/idl/dds4ccm_ConnectorStatusListener.idl3"
#include "connectors/dds4ccm/idl/dds4ccm_ConnectorStatusListenerE.idl"
#include "connectors/dds4ccm/idl/dds4ccm_ListenerControl.idl3"
#include "connectors/dds4ccm/idl/dds_rtf2_dcpsE.idl"
#include "Base/Quoter_Base.idl"

module CCM_DDS
{
  // @from DDS_Write for Quoter::Stock_Info
  interface Stock_Info_Writer
  {
    void write (in Quoter::Stock_Info an_instance)
      raises (InternalError);
  };

  // @from DDS_Update for Quoter::Stock_Info
  interface Stock_Info_Updater
  {
    void create (in Quoter::Stock_Info an_instance)
        raises (AlreadyCreated,
                InternalError);
    void update(in Quoter::Stock_Info an_instance)
        raises (NonExistent,
                InternalError);
    void delete (in Quoter::Stock_Info an_instance)
        raises (NonExistent,
                InternalError);
    readonly attribute boolean is_lifecycle_checked;
  };

  interface Stock_Info_Getter
  {
    boolean get_all (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
            raises (InternalError);
    boolean get_all_history (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
            raises (InternalError);
    boolean get_one (inout Quoter::Stock_Info an_instance, out ReadInfo info)
            raises (NonExistent,
                    InternalError);
    boolean get_one_history (in Quoter::Stock_Info an_instance,
                           out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
            raises (NonExistent,
                    InternalError);
    boolean get_next (out Quoter::Stock_Info an_instance, out ReadInfo info)
            raises (InternalError);
    attribute QueryFilter filter
            setraises (BadParameter);
    attribute DDS::Duration_t time_out;
  };

  // @from Reader<T> from DDS_RawListen for Quoter::Stock_Info
  interface Stock_Info_Reader
  {
    void read_all (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
      raises (InternalError);

    void read_all_history (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
      raises (InternalError);

    void read_one (inout Quoter::Stock_Info an_instance, out ReadInfo info)
      raises (NonExistent,
              InternalError);

    void read_one_history (in Quoter::Stock_Info an_instance,
                           out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
      raises (NonExistent,
              InternalError);

    attribute QueryFilter filter
      setraises (BadParameter);
  };

  // @from RawListener for Quoter::Stock_Info
  interface Stock_Info_RawListener
  {
    void on_data (in Quoter::Stock_Info an_instance, in ReadInfo info);
  };

  porttype DDS_Write {
    uses CCM_DDS::Stock_Info_Writer data;
    uses DDS::DataWriter    dds_entity;
  };
  porttype DDS_Update {
    uses CCM_DDS::Stock_Info_Updater data;
  };
  porttype DDS_Listen {
    uses CCM_DDS::Stock_Info_Reader data;
    uses CCM_DDS::Stock_Info_Getter get_data;
    uses CCM_DDS::ListenerControl data_control;
    provides CCM_DDS::Stock_Info_RawListener data_listener;
    uses DDS::DataReader dds_entity;
    provides CCM_DDS::PortStatusListener status;
  };
};

module Quoter
{
  component Quoter_Connector //: CCM_DDS::DDS_TopicBase
  {
    // @from DDS_TopicBase
    attribute string topic_name;
    readonly attribute DDS::StringSeq key_fields;

    // @from DDS_TopicBase : DDS_Base
    uses CCM_DDS::ConnectorStatusListener error_listener;
    attribute DDS::DomainId_t domain_id;
    attribute string qos_profile;

    // @from DDS_Write for Stock_Info
    mirrorport CCM_DDS::DDS_Write info_in;

    // @from DDS_Update for Stock_Info
    mirrorport CCM_DDS::DDS_Update info_update;

    // @from DDS_RawListen for Stock_Info
    mirrorport CCM_DDS::DDS_Listen info_out;
  };
};

/*
#include <dds4ccm/idl/dds4ccm_Connector_Base.idl3p>
#include <dds4ccm/idl/dds4ccm_Port_RawListen.idl3p>
#include <dds4ccm/idl/dds4ccm_Port_Write.idl3p>

module Quoter
{
  connector Quoter_Connector : CCM_DDS::DDS_TopicBase
  {
    mirrorport CCM_DDS::DDS_Write <Stock_Info> info_in;
    mirrorport CCM_DDS::DDS_RawListen <Stock_Info> info_out;
  };
};

 */

#endif /* QUOTER_CONNECTOR_IDL3_ */
