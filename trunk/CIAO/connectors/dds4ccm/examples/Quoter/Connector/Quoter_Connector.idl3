// $Id$

/**
 * @file Quoter_Connector.idl3
 * @author William R. Otte <wotte@dre.vanderbilt.edu>
 *
 * by-hand translation of the idl3p.
 */

#ifndef QUOTER_CONNECTOR_IDL3_
#define QUOTER_CONNECTOR_IDL3_

#include  <Components.idl>
#include "dds4ccm/idl/dds4ccm_ListenerControl.idl3"
#include "dds4ccm/idl/dds4ccm_ListenerControlE.idl"
#include "dds4ccm/idl/dds4ccm_PortStatusListener.idl3"
#include "dds4ccm/idl/dds4ccm_PortStatusListenerE.idl"
#include "dds4ccm/idl/dds4ccm_ConnectorStatusListener.idl3"
#include "dds4ccm/idl/dds4ccm_ConnectorStatusListenerE.idl"
#include "dds4ccm/idl/dds4ccm_ListenerControl.idl3"
#include "dds4ccm/idl/dds_rtf2_dcpsE.idl"
#include "Base/Quoter_Base.idl"

module CCM_DDS
{
  // @from DDS_Write for Quoter::Stock_Info
  interface Stock_Info_Writer
  {
    void write (in Quoter::Stock_Info an_instance)
      raises (InternalError);
  };

  // @from DDS_Update for Quoter::Stock_Info
  interface Stock_Info_Updater
  {
    void create (in Quoter::Stock_Info an_instance)
        raises (AlreadyCreated,
                InternalError);
    void update(in Quoter::Stock_Info an_instance)
        raises (NonExistent,
                InternalError);
    void delete (in Quoter::Stock_Info an_instance)
        raises (NonExistent,
                InternalError);
    readonly attribute boolean is_lifecycle_checked;
  };
  
  interface Stock_Info_Getter
  {
    boolean get_all (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
            raises (InternalError);
    boolean get_all_history (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
            raises (InternalError);
    boolean get_one (inout Quoter::Stock_Info an_instance, out ReadInfo info)
            raises (NonExistent,
                    InternalError);
    boolean get_one_history (in Quoter::Stock_Info an_instance,
                           out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
            raises (NonExistent,
                    InternalError);
    boolean get_next (out Quoter::Stock_Info an_instance, out ReadInfo info)
            raises (InternalError);
    attribute QueryFilter filter
            setraises (BadParameter);
    attribute DDS::Duration_t time_out;
  };
  
  // @from Reader<T> from DDS_RawListen for Quoter::Stock_Info
  interface Stock_Info_Reader
  {
    void read_all (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
      raises (InternalError);

    void read_all_history (out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
      raises (InternalError);

    void read_one (inout Quoter::Stock_Info an_instance, out ReadInfo info)
      raises (NonExistent,
              InternalError);

    void read_one_history (in Quoter::Stock_Info an_instance,
                           out Quoter::Stock_Info_Seq instances, out ReadInfoSeq infos)
      raises (NonExistent,
              InternalError);

    attribute QueryFilter filter
      setraises (BadParameter);
  };

  // @from RawListener for Quoter::Stock_Info
  interface Stock_Info_RawListener
  {
    void on_data (in Quoter::Stock_Info an_instance, in ReadInfo info);
  };
  
  
};

module Quoter
{
  component Quoter_Connector //: CCM_DDS::DDS_TopicBase
  {
    // @from DDS_TopicBase
    attribute string topic_name;
    readonly attribute DDS::StringSeq key_fields;

    // @from DDS_TopicBase : DDS_Base
    uses CCM_DDS::ConnectorStatusListener error_listener;
    attribute DDS::DomainId_t domain_id;
    attribute string qos_profile;

    // @from DDS_Write for Stock_Info
    provides CCM_DDS::Stock_Info_Writer info_in_data;
    provides DDS::DataWriter info_in_dds_entity;

    // @from DDS_Update for Stock_Info
    provides CCM_DDS::Stock_Info_Updater info_update_data;

    // @from DDS_RawListen for Stock_Info
    provides CCM_DDS::Stock_Info_Reader info_out_data;
    provides CCM_DDS::Stock_Info_Getter info_get_out_data;
    provides CCM_DDS::ListenerControl info_out_control;
    uses CCM_DDS::Stock_Info_RawListener info_out_listener;
    provides DDS::DataReader info_out_dds_entity;
    uses CCM_DDS::PortStatusListener info_out_status;
    uses CCM_DDS::ConnectorStatusListener info_out_connector_status;
  };
};

#endif /* QUOTER_CONNECTOR_IDL3_ */
