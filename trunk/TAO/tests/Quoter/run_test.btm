@echo off

REM This is a 4NT Batch file.  To use, just type run_test and it will start the 
REM server, wait for a second, and then start the client.  At the end, it will
REM clean up.  

setlocal

set nsiorfile=%temp%\qns_ior
set del=2

REM variables for parameters
set n=1
set leave=/C
set ior=0

:get_args

if %1 == /h goto is_h
if %1 == /? goto is_h
goto no_h
:is_h
echo run_test [/n num] [/leave] [/h]
echo.
echo /n num  -- runs the client num times
echo /leave  -- leaves the servers running and their windows open
echo /h      -- prints this information
quit
:no_h

if not %1 == /n goto no_n
set n=%2
shift
shift
goto get_args
:no_n

if not %1 == /leave goto no_leave
set leave=/K
shift
goto get_args
:no_leave

REM Naming Service must have been built or else this will fail
start "Naming Service" %leave ..\..\orbsvcs\Naming_Service\Naming_Service.exe -ORBobjrefstyle url -o %nsiorfile
rem -ORBnameserviceior %temp\qns_ior

REM Delay some to let the Naming Service get set up
DELAY %del

REM read in the IOR from the file
for %%d in (@%nsiorfile) do set ior=%%d

REM Start the server
start "Quoter Server" %leave Quoter_Server.exe -ORBobjrefstyle url -ORBnameserviceior %ior -ORBsvcconf server.conf 

REM Delay some to let the server get set up
DELAY %del

REM Start the factory finder
start "Quoter Factory Finder" %leave QuoterFactoryFinder.exe -ORBobjrefstyle url -ORBnameserviceior %ior -ORBsvcconf svc.conf

REM Delay some to let the Factory Finder get set up
DELAY %del

REM And now start the client
for /l %x in (1,1,%n) do Quoter_Client.exe -ORBobjrefstyle url -ORBnameserviceior %ior -ORBsvcconf client.conf

REM We don't really need to delete this
REM del %temp%\qns_ior

REM Remove any running servers
if %leave==/C activate "Naming Service" CLOSE
if %leave==/C activate "Quoter Server" CLOSE
if %leave==/C activate "Quoter Factory Finder" CLOSE

endlocal