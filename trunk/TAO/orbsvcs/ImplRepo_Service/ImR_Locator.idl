// -*- IDL -*-
// $Id$

#include "ImR_Activator.idl"

module ImplementationRepository
{
  interface Locator : AdministrationExt
  {
    /// returns a token that can be used (along with activator name) to
    /// unregister the activator.
    long register_activator (in string name, in Activator act);

    /// You must pass in the token returned from register_activator.
    void unregister_activator (in string name, in long token);

    /// The ImR_Activator calls this method to notify death of child
    /// process that it had started.
    void notify_child_death (in string name);

    /// Newer Activators call this method to notify death of child
    /// process including pid to disambiguate child instances
    void child_death_pid (in string name, in long pid);

    /// Newer Activators call this method to notify spawn of child
    /// process to supply pid for reference
    void spawn_pid (in string name, in long pid);


  };

  enum UpdateType
  {
    repo_add,
    repo_update,
    repo_remove
  };

  typedef long SequenceNum;

  struct ServerUpdate
  {
    string      name;
    string      server_id;
    UpdateType  type;
    long        repo_id;
    long        repo_type;
    SequenceNum seq_num;
  };

  struct ActivatorUpdate
  {
    string      name;
    UpdateType  type;
    long        repo_id;
    long        repo_type;
    SequenceNum seq_num;
  };

  enum AAM_Status
  {
    AAM_INIT,
    AAM_SERVER_STARTED_RUNNING,
    AAM_ACTIVATION_SENT,
    AAM_WAIT_FOR_RUNNING,
    AAM_WAIT_FOR_PING,
    AAM_WAIT_FOR_ALIVE,
    AAM_WAIT_FOR_DEATH,
    AAM_SERVER_READY,
    AAM_SERVER_DEAD,
    AAM_NOT_MANUAL,
    AAM_NO_ACTIVATOR,
    AAM_NO_COMMANDLINE
  };

  struct AccessStateUpdate
  {
    string    name;
    AAM_Status state;
  };

  exception InvalidPeer
  {
    string reason;
  };

  interface UpdatePushNotification
  {
    /// provide information about access request status to allow primary
    /// and backup ImR to both
    oneway void notify_access_state_update (in AccessStateUpdate server);

    /// indicate existing server with name was updated
    oneway void notify_updated_server(in ServerUpdate server);

    /// indicate existing activator with name was updated
    //oneway void notify_updated_activator(in ActivatorUpdate activator);
    oneway void notify_updated_activator(in ActivatorUpdate activator);

    /// register peer as replica and indicate sequence number
    void register_replica(in UpdatePushNotification peer,
                          inout string ft_imr_ior,
                          out SequenceNum seq_num)
      raises (InvalidPeer);
  };
};

