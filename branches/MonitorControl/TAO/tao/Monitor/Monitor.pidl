#include "tao/TimeBase.pidl"
#include "tao/StringSeq.pidl"

module Monitor
{
  interface MC
  {
    struct Data
    {
      /// The number of samples currently taken into account
      TimeBase::TimeT timestamp;
      double value;
    };

    typedef string Name;
    typedef CORBA::StringSeq NameList;
    typedef sequence<Data> DataList;
    struct DataItem
    {
      Name itemname;
      DataList dlist;
    };
    typedef sequence<DataItem> DataListList;

    exception InvalidName {
      NameList names;
    };

    NameList get_statistic_names ();

    /**
     * Get the statistics for the given monitor points. If @a names
     * contains an invalid name an exception is raised with all
     * invalid names
     */
    DataListList get_statistics (in NameList names)
      raises (InvalidName);

    /**
     * Get and clear the statistics for the given monitor points.
     * If @a names contains an invalid name no statistics are cleared
     */
    DataListList get_and_clear_statistics (in NameList names)
      raises (InvalidName);

    /**
     * Clear the statistics for the given monitor points.
     * If @a names contains an invalid name no statistics are cleared
     */
    void clear_statistics (in NameList names)
      raises (InvalidName);
  };
};
