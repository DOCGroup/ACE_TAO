/// $Id$
/// Compliant with OMG 06-04-01 Section 6.5.3

#if !defined (CCM_RECEPTACLE_IDL)
#define CCM_RECEPTACLE_IDL

#include "ccm/CCM_Base.idl"
#include "ccm/CCM_Navigation.idl"
#include "ccm/CCM_Cookie.idl"

module Components
{
  typeprefix Components "omg.org";

  valuetype ConnectionDescription
  {
    public Cookie ck;
    public Object objref;
  };
  typedef sequence<ConnectionDescription> ConnectionDescriptions;

  valuetype ReceptacleDescription : PortDescription
  {
    public boolean is_multiple;
    public ConnectionDescriptions connections;
  };
  typedef sequence<ReceptacleDescription> ReceptacleDescriptions;

  exception ExceededConnectionLimit {};
  exception CookieRequired {};

  interface Receptacles
  {
    Cookie connect (in FeatureName name, in Object connection )
      raises (InvalidName,
              InvalidConnection,
              AlreadyConnected,
              ExceededConnectionLimit);

    Object disconnect (in FeatureName name, in Cookie ck)
      raises (InvalidName,
              InvalidConnection,
              CookieRequired,
              NoConnection);

    ConnectionDescriptions get_connections (in FeatureName name)
      raises (InvalidName);

    ReceptacleDescriptions get_all_receptacles ();

    ReceptacleDescriptions get_named_receptacles (in NameList names)
      raises (InvalidName);
  };
};
#endif /* CCM_RECEPTACLE_IDL */
