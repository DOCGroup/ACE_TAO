// $Id$
#ifndef DOMAINAPPLICATIONMANAGER_IDL
#define DOMAINAPPLICATIONMANAGER_IDL

#include "DAnCE/Deployment/Deployment.idl"
#include "DAnCE/Deployment/Deployment_NodeApplication.idl"
#include "DAnCE/Deployment/Deployment_ApplicationManager.idl"

module Deployment {
  // @@changed
  // interface DomainApplicationManager :
  //     ApplicationManager
  //   {
  //     Applications getApplications ();
  //     DeploymentPlan getPlan ();
  //   };
  
  /// CIAO 's DomainApplicationManager interface differs spec-defined interface
  /// in the same that it also integrates the spec-defined interface for
  /// DomainApplication into itself
  /// This should be derived from ApplicationManager
  interface DomainApplicationManager
  {
    DeploymentPlan getPlan ();

    /// This method is missing from CIAO but in the spec
    /// Applications getApplications ();

    /// CIAO specific version of startLaunch implementation, this one
    /// differs with spec-defined operation in the sense that it doesn't
    /// return DomainApplication object reference.
    void startLaunch (in Properties configProperty, in boolean start)
      raises (ResourceNotAvailable, StartError, InvalidProperty);

    /// This method has CIAO specific arguments and is not spec compliant
    void finishLaunch (in boolean start,
                       in boolean is_ReDAC)
      raises (StartError, InvalidConnection);

    /// CIAO specific extension
    void start ()
      raises (StartError);

    /// CIAO specific extention
    /// Fetch NodeApplication based on the given node name
    NodeApplication get_node_app (in string node_name)
      raises (NoSuchName);

    /// CIAO specific extension, it differs with the spec defined one
    /// for no input parameter.
    void destroyApplication ()
      raises (StopError);

    /// CIAO specific extension to destroy NodeApplicationManager
    void destroyManager ()
      raises (StopError);

    /// CIAO specific extension
    /// Perform the actual redeployment and reconfiguration on the dommain level.
    void perform_redeployment (in DeploymentPlan new_plan)
      raises (PlanError,
              InstallationFailure,
              UnknownImplId,
              ImplEntryPointNotFound,
              InvalidConnection,
              InvalidProperty,
              ::Components::RemoveFailure);
  };

  typedef sequence < DomainApplicationManager > DomainApplicationManagers;
};

#endif /* DOMAINAPPLICATIONMANAGER_IDL */
