// $Id$

/**
 * @file CIAO_Events.idl
 *
 * @author George Edwards
 *
 * @brief Interfaces for configuring CIAO's event mechanism.
 */

#include "CCM_Event.idl"
#include "RtecEventChannelAdmin.idl"
#include "CosNotifyChannelAdmin.idl"

module CIAO_Events
{
  /// A component's UUID.
  typedef long CONNECTION_ID;

  enum EventServiceType
  {
    DIRECT,
    EC,
    RTEC,
    NOTIFY,
    RTNOTIFY
  };

  /// An object for setting consumer QoS requirements. This will be expanded to
  /// allow filtering, correlation, etc. A Consumer_Config object is passed as
  /// a parameter to CIAO::Container::_ciao_connect_event_consumer ().
  /// Consumer_Config objects are created via
  /// CIAO::Container::_ciao_create_event_consumer_config ().
  interface Consumer_Config
  {

    // Configuring methods.
    void start_conjunction_group (in long size);

    void start_disjunction_group (in long size);

    void insert_supplier_id (in CONNECTION_ID connection_id);

    void set_consumer_id (in CONNECTION_ID connection_id);

    void set_consumer (in Components::EventConsumerBase consumer);

    // Access methods.
    CONNECTION_ID get_consumer_id ();

    CONNECTION_ID get_supplier_id ();

    Components::EventConsumerBase get_consumer ();

    EventServiceType get_service_type ();

    RtecEventChannelAdmin::ConsumerQOS get_rt_event_qos ();

    CosNotifyFilter::Filter get_notify_filter ();

    CosNotification::QoSProperties get_notify_qos ();
  };


  /// An object for setting supplier QoS requirements.
  /// Passed as a parameter to CIAO::Container::_ciao_connect_event_supplier ().
  /// Created via CIAO::Container::_ciao_create_event_supplier_config ().
  interface Supplier_Config
  {

    // Configuring methods.
    void set_supplier_id (in CONNECTION_ID connection_id);

    // Access methods.
    CONNECTION_ID get_supplier_id ();

    EventServiceType get_service_type ();

    RtecEventChannelAdmin::SupplierQOS get_rt_event_qos ();

    CosNotifyFilter::Filter get_notify_filter ();

    CosNotification::QoSProperties get_notify_qos ();
  };

};
