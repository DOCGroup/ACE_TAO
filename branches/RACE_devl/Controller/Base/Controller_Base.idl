#ifndef RACE_CONTROLLER_BASE_IDL
#define RACE_CONTROLLER_BASE_IDL

/**
 * @file Controller.idl
 *
 * @brief Base Controller Component of RACE.
 *
 * This file declares a Base Controller of the RACE framework.
 *
 * @author Nishanth Shankaran <nshankar@nospam.com>
 */

#include <Components.idl>
#include "TM_Proxy/TM_Proxy.idl"
#include "common/OpString.idl"
#include "common/RACE_common.idl"
#include "Effectors/Base/Effector_Base.idl"

module CIAO
{
  module RACE
  {
    module Controller
    {

      interface Admin
      {
        /// Initialize the controller.
        boolean init_controller ();

        /// Start the controller.
        boolean start_controller ();

        /// Start the controller.
        boolean stop_controller ();
      };

      interface Control_Operations
      {
        /// "Register" a string with the controller.
        boolean register_string (in OperationalString opstring,
                                 out UUID ID);

        /// "Unregister" a string. Raises UnknownID.
        boolean unregister_string (in UUID ID)
          raises (UnknownID);
      };

      component Controller_Base
      {
        /// Obtain system information (both resource utilization and QoS
        /// infromation) from the target manager.
        uses ::CIAO::RACE::TM_Proxy::SystemSnapshot system_monitor;

        uses ::CIAO::RACE::TM_Proxy::Utility system_utils;

        uses ::CIAO::RACE::Effector::ApplicationActuator appActuator;

        uses ::CIAO::RACE::Effector::SystemActuator sysActuator;

        provides Control_Operations operations ;

        provides Admin admin;

        /// The sampling period of the controller.
        attribute long sampling_period;
      };
    };
  };
};
#endif /* RACE_CONTROLLER_IDL */
