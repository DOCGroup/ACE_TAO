// -*- MPC -*-
// $Id$

project {
  // Set the idl compiler and flags as template values
  specific {
    tao_idl      = $(ACE_ROOT)/bin/tao_idl
    tao_idl_dep  = $(ACE_ROOT)/bin/tao_idl
    tao_idlflags = -Ge 1 -Wb,pre_include=ace/pre.h \
                   -Wb,post_include=ace/post.h -I$(TAO_ROOT)
  }

  Define_Custom(IDL) {
    automatic            = 1
    dependent            = $(TAO_IDL_DEP)
    command              = $(TAO_IDL)
    libpath              = $(ACE_ROOT)/lib
    inputext             = .idl
    source_pre_extension = C, S
    header_pre_extension = C, S
    inline_pre_extension = C, S
    source_outputext     = .cpp, .cxx, .cc, .C
    header_outputext     = .h, .hpp, .hxx, .hh
    inline_outputext     = .inl, .i
    keyword idlflags     = commandflags

    optional(source_pre_extension) {
      commandflags(-GA) += A
    }
    optional(template_outputext) {
      commandflags(!-Sc) += S_T.cpp, S_T.cxx, S_T.cc, S_T.C
    }
    optional(header_pre_extension) {
      commandflags(!-Sc) += S_T
    }
    optional(inline_pre_extension) {
      commandflags(!-Sc) += S_T
    }
  }

  after    += TAO_IDL_EXE
  idlflags += $(TAO_IDLFLAGS)
}
